set(CENTOS 1)
set(USE_CRT "")
set(TARGET_VENDOR -redhat)
include(${CMAKE_CURRENT_LIST_DIR}/linux.clang.cmake)

if(NOT RHEL_MAJOR)
  file(STRINGS "${CMAKE_SYSROOT}/usr/include/linux/version.h" centos_ver_str REGEX "^#[\t ]*define[\t ]+RHEL_M[A-Z]*[\t ]+[0-9]+$")
  foreach(VLINE ${centos_ver_str})
    if(VLINE MATCHES "^#[\t ]*define[\t ]+RHEL_MAJOR[\t ]+([0-9]+)$")
      set(RHEL_MAJOR "${CMAKE_MATCH_1}")
    endif()
    if(VLINE MATCHES "^#[\t ]*define[\t ]+RHEL_MINOR[\t ]+([0-9]+)$")
      set(RHEL_MINOR "${CMAKE_MATCH_1}")
    endif()
  endforeach()
  if(RHEL_MAJOR GREATER 0)
    set(CMAKE_SYSTEM_VERSION "${RHEL_MAJOR}.${RHEL_MINOR}")
  endif()
endif()
# centos8 gdb8 supports dwarf5
if(RHEL_MAJOR LESS 8)
  add_compile_options($<$<AND:$<COMPILE_LANGUAGE:C,CXX>,$<CONFIG:Debug,RelWithDebInfo>>:-gdwarf-4>)
endif()